buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'

sourceCompatibility = 1.8
version = '1.0'

repositories {
    jcenter()
}

configurations {
    querydslapt
}

dependencies {
    querydslapt "com.querydsl:querydsl-apt:$querydsl_version"

    compile 'org.springframework.boot:spring-boot-devtools',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            "com.querydsl:querydsl-jpa:$querydsl_version"

    runtime 'org.flywaydb:flyway-core'

    testCompile 'org.springframework.boot:spring-boot-starter-test'

    // hsql version 2.3.4 breaks flyway migrations
    // https://sourceforge.net/p/hsqldb/bugs/1441/
    // https://github.com/flyway/flyway/issues/1346
    // https://github.com/spring-projects/spring-boot/issues/6394
    testRuntime 'org.hsqldb:hsqldb:2.3.3:jdk6debug'
}

def queryDslSources = "$buildDir/src/querydsl/java"
sourceSets.main.java.srcDir queryDslSources

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = file(queryDslSources)
}

compileJava.dependsOn generateQueryDSL
clean.dependsOn cleanGenerateQueryDSL

bootRepackage {
    enabled = false
}

test.testLogging.lifecycle {
    events 'skipped', 'passed', 'failed'
}

wrapper {
    gradleVersion = '3.0'
}

