apply plugin: 'java'

sourceCompatibility = 1.5
version = '1.0'

sourceSets.main.java.srcDir 'src/main/generated'

repositories {
    mavenCentral()
}

configurations {
    querydslapt
}

dependencies {
    querydslapt 'com.mysema.querydsl:querydsl-apt:3.2.4'

    compile 'org.springframework.data:spring-data-jpa:1.4.3.RELEASE'
    compile 'org.springframework:spring-context:3.2.3.RELEASE'
    compile 'org.springframework:spring-orm:3.2.3.RELEASE'
    compile 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
    compile 'com.googlecode.flyway:flyway-core:2.1.1'
    compile 'com.mysema.querydsl:querydsl-jpa:3.2.4'

    runtime 'ch.qos.logback:logback-classic:1.0.13'
    runtime 'cglib:cglib:3.1'
    runtime 'org.hibernate:hibernate-entitymanager:4.2.7.Final'

    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.springframework:spring-test:3.2.3.RELEASE'

    testRuntime 'org.hsqldb:hsqldb:2.3.1'
    testRuntime 'org.springframework:spring-core:3.2.3.RELEASE'
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = file('src/main/generated')
}

compileJava.dependsOn generateQueryDSL
clean.dependsOn cleanGenerateQueryDSL