apply plugin: 'java'

sourceCompatibility = 1.8
version = '1.0'

sourceSets.main.java.srcDir 'src/main/generated'

repositories {
    mavenCentral()
}

configurations {
    querydslapt
}

def spring_jpa_version = '1.5.1.RELEASE'
def spring_framework_version = '4.0.2.RELEASE'
def querydsl_version = '3.2.4'

dependencies {
    querydslapt 'com.mysema.querydsl:querydsl-apt:' + querydsl_version

    compile 'org.springframework.data:spring-data-jpa:' + spring_jpa_version
    compile 'org.springframework:spring-context:' + spring_framework_version
    compile 'org.springframework:spring-orm:' + spring_framework_version
    compile 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
    compile 'com.googlecode.flyway:flyway-core:2.1.1'
    compile 'com.mysema.querydsl:querydsl-jpa:' + querydsl_version

    runtime 'ch.qos.logback:logback-classic:1.0.13'
    runtime 'cglib:cglib:3.1'
    runtime 'org.hibernate:hibernate-entitymanager:4.2.7.Final'

    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.springframework:spring-test:' + spring_framework_version

    testRuntime 'org.hsqldb:hsqldb:2.3.1'
    testRuntime 'org.springframework:spring-core:' + spring_framework_version
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = file('src/main/generated')
}

compileJava.dependsOn generateQueryDSL
clean.dependsOn cleanGenerateQueryDSL