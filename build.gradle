buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}",
                "no.nils:wsdl2java:0.10"
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'

sourceCompatibility = 1.8
version = '1.0'

sourceSets.main.java.srcDir 'src/main/generated'

repositories {
    jcenter()
}

configurations {
    querydslapt
}

dependencies {
    querydslapt "com.querydsl:querydsl-apt:$querydsl_version"

    compile 'org.springframework.boot:spring-boot-devtools',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            "com.querydsl:querydsl-jpa:$querydsl_version"

    testCompile 'org.springframework.boot:spring-boot-starter-test'

    testRuntime 'org.hsqldb:hsqldb:2.3.4:jdk6debug'
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = file('src/main/generated')
}

compileJava.dependsOn generateQueryDSL
clean.dependsOn cleanGenerateQueryDSL

bootRepackage {
    enabled = false
}

wrapper {
    gradleVersion = '3.0'
}

